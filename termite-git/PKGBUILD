# Maintainer: Daniel Micay <danielmicay@gmail.com>
# Contributor: Jared Rickert <jaredrickert52@gmail.com>

pkgname=('termite' 'termite-terminfo')
pkgver=14
pkgrel=3
arch=('i686' 'x86_64')
url="https://github.com/jlrickert/termite/"
license=('LGPL')
makedepends=('git' 'vte3-ng' 'ncurses')
source=("git://github.com/jlrickert/termite")
md5sums=('SKIP')

build() {
  cd $pkgbase
  git submodule update --init
  make
}

package_termite() {
  pkgdesc="A simple VTE-based terminal"
  depends=('vte3-ng' 'termite-terminfo')
  backup=(etc/xdg/termite/config)

  cd $pkgbase
  make PREFIX=/usr DESTDIR="$pkgdir" install
  rm -rf "$pkgdir/usr/share/terminfo"
  install -Dm644 config "$pkgdir/etc/xdg/termite/config"
}

package_termite-terminfo() {
  pkgdesc='Terminfo for Termite, a simple VTE-based terminal'

  cd $pkgbase
  install -d "$pkgdir/usr/share/terminfo"
  tic -x termite.terminfo -o "$pkgdir/usr/share/terminfo"
}
